apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

description = 'LBU-LTI'
group = 'io.github.leeds-beckett-digital-learning'

// Publish javadoc and source packages along with built library
java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'javax.servlet:javax.servlet-api:4.0.0'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.apache.commons:commons-text:1.12.0'

    // The generic java cache API is included here but the standard
    // Java runtime will not have a cache provider installer so an
    // application that uses this library will need to include a
    // cache provider.
    implementation 'javax.cache:cache-api:1.1.1'

    // Needed because LTI configuration is stored in JSON format.
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
    
    // Because this is a library and not an application
    // only the api library from jsonwebtoken is needed here
    // Apps and frameworks will need to include the implementation library
    // and possibly other runtime dependencies
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
}

signing {
    sign(publishing.publications)
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
      pom {
          name = rootProject.name
          description = 'A Java library to support applications with LTI 1.3'
          url = 'https://github.com/leeds-beckett-digital-learning/lbu-lti'
          licenses {
              license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              }
          }
          developers {
              developer {
                  id = 'maber01'
                  name = 'Jon Maber'
                  email = 'j.r.maber@leedsbeckett.ac.uk'
              }
          }
          scm {
              connection = 'scm:git:https://github.com/leeds-beckett-digital-learning/lbu-lti.git'
              developerConnection = 'scm:git:git@github.com:leeds-beckett-digital-learning/lbu-lti.git'
              url = 'https://github.com/leeds-beckett-digital-learning/lbu-lti'
          }
      }
    }
  }
  repositories {
    maven {
      name = "OSSRH"
      url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUserName
        password = ossrhToken
      }
    }
  }
}
